FUNCTION "fcCalcAssign" : Void

{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
VAR_INPUT
  fValue : Real;
END_VAR

VAR_OUTPUT

END_VAR

VAR_IN_OUT
  sTag : "stCalcValue";
END_VAR

VAR_TEMP

END_VAR

VAR CONSTANT

END_VAR


BEGIN
	// When assigning to a calc value, set sTag.fValue and bits
	
	IF "isNaN"(#fValue) THEN
	    // Set error on NaN in
	    #sTag.bError := TRUE;
	    #sTag.fValue := #fValue;
	    #sTag.bLimited := FALSE;
	ELSE
	    // Valid values
	    #sTag.bError := FALSE;
	    IF #sTag.bCheckLimits = TRUE THEN
	        // With limitation
	        #sTag.fValue := LIMIT(IN := #fValue, MN := #sTag.fMin, MX := #sTag.fMax);
	        #sTag.bLimited := #fValue <> #sTag.fValue;
	    ELSE
	        // Without limitation
	        #sTag.fValue := #fValue;
	        #sTag.bLimited := FALSE;
	    END_IF;
	END_IF;
END_FUNCTION

