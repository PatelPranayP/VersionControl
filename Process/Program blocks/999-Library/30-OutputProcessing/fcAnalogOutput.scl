FUNCTION "fcAnalogOutput" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      bOutputOk : Bool;   // 1 = output channel/module/fieldbus device is working, 0 = not working
   END_VAR

   VAR_OUTPUT 
      iAO : Int;   // Raw value to write
   END_VAR

   VAR_IN_OUT 
      sTag : "stAnalogOutput";   // Output channel scaling parameters
   END_VAR

   VAR_TEMP 
      fSlope : Real;
      iIntercept : Int;
   END_VAR


BEGIN
	// Scale a CalcValue to a raw value
	
	// Output channel status. Scale always even if output channel is not ok
	#sTag.Raw.bFault := NOT(#bOutputOk);
	// Scaling. Only intervene if the value is invalid or NaN
	IF #sTag.Eng.bError = TRUE OR "isNaN"(#sTag.Eng.fValue) THEN
	    #sTag.Raw.iValue := 0;
	ELSE
	    // NOTE: not detecting division by zero here
	    #fSlope := INT_TO_REAL(#sTag.Raw.iMax - #sTag.Raw.iMin) / (#sTag.Eng.fMax - #sTag.Eng.fMin);
	    #iIntercept := #sTag.Raw.iMax - REAL_TO_INT(#fSlope * #sTag.Eng.fMax);
	    #sTag.Raw.iValue := REAL_TO_INT(#sTag.Eng.fValue*#fSlope) + #iIntercept;
	END_IF;
	
	// Local outputs
	#iAO := #sTag.Raw.iValue;
END_FUNCTION

