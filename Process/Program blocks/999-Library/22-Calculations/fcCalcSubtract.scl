FUNCTION "fcCalcSubtract" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      sInput1 : "stCalcValue";
      sInput2 : "stCalcValue";
   END_VAR

   VAR_IN_OUT 
      sTag : "stCalcValue";
   END_VAR

   VAR_TEMP 
      fResult : Real;
   END_VAR


BEGIN
	// Subtract two Calc values
	// A subtraction is not defined if either or both arguments are not defined, or more directly:
	// If one or both values is #NaN, the result should always be #NaN.
	
	IF #sInput1.bError = TRUE OR #sInput2.bError = TRUE OR "isNaN"(In := #sInput1.fValue) OR "isNaN"(In := #sInput2.fValue) THEN
	    #sTag.bError := TRUE;
	    #sTag.fValue := "NaN"();
	ELSE
	    #sTag.bError := FALSE;
	    // Do the subtraction
	    #fResult := #sInput1.fValue - #sInput2.fValue;
	    // Handle limiting
	    "fcCalcAssign"(fValue := #fResult, sTag := #sTag);
	END_IF;
	
	
END_FUNCTION

