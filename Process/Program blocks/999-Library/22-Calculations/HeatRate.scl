FUNCTION "HeatRate" : Real
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      Pressure_High : Real;   // Bar
      Temperature_High : Real;   // C
      EvaporationTemperature_High : Real;   // C
      LiquidState_High : Bool;   // 1 = liquid, 0 = gas
      Pressure_Low : Real;   // Bar
      Temperature_Low : Real;   // C
      EvaporationTemperature_Low : Real;   // C
      LiquidState_Low : Bool;   // 1 = liquid, 0 = gas
      Massflow : Real;   // kg/h
   END_VAR

   VAR_TEMP 
      H_High : Real;   // High value of Enthalpy
      H_Low : Real;   // Low value of Enthalpy
   END_VAR


BEGIN
	// The difference of the enthalpy values times massflow is the heat rate Qdot
	// Use the "High" group of values for the side of the enthalpy source/sink with the greatest magnitude, so as to get positive values
	
	#H_High := "Enthalpy_f"(Pressure := #Pressure_High,
	                        Themperature := #Temperature_High,
	                        EvaporationTemperature := #EvaporationTemperature_High, 
	                        Gas := NOT #LiquidState_High, 
	                        Liquide := #LiquidState_High);
	    
	#H_Low := "Enthalpy_f"(Pressure := #Pressure_Low,  
	                       Themperature := #Temperature_Low,
	                       EvaporationTemperature := #EvaporationTemperature_Low, 
	                       Gas := NOT #LiquidState_Low, 
	                       Liquide := #LiquidState_Low);
	
	// Convert Massflow into kg/s
	#HeatRate := (#H_High - #H_Low) * (#Massflow / 3600.0);
END_FUNCTION

