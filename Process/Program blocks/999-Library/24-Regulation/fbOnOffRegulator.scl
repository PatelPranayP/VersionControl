FUNCTION_BLOCK "fbOnOffRegulator"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      bEnable : Bool;   // Enable from process. 1 = enabled, 0 = disabled
      sPV : "stCalcValue";   // Process value
      Above : Bool;   // 1 = regulate when above threshold, 0 = regulate when below threshold
      bReset : Bool;   // 1 = reset manual modes
   END_VAR

   VAR_OUTPUT 
      bOutput : Bool;
      bAtSetpoint : Bool;
   END_VAR

   VAR_IN_OUT 
      sTag : "stOnOffRegulator";
   END_VAR

   VAR 
      Controller { S7_SetPoint := 'False'} : "fbHysteresis";
   END_VAR

   VAR_TEMP 
      fSetpoint : Real;
      Enabled : Bool;
   END_VAR


BEGIN
	// 1-sided on/off controller
	// Above = 1: regulate when Process Value (PV) above threshold. Eg. cooling, emptying, depressurizing
	// Above = 0: regulate when PV below threshold. Eg. heating, filling, pressurizing
	
	// Reset manual modes.
	IF #bReset THEN
	    #sTag.bManualMode := FALSE;
	    #sTag.fManualSetpoint := 0.0;
	    #sTag.bManualOverride := FALSE;
	    #sTag.bOverrideCmd := FALSE;
	END_IF;
	
	IF #sTag.bManualOverride = TRUE THEN
	    // In manual override, everything is awesome
	    #sTag.bAtSetpoint := TRUE;
	    #sTag.bFault := FALSE;
	    #sTag.bOutput := #sTag.bOverrideCmd;
	ELSE
	    // Choose setpoint
	    IF #sTag.bManualMode = TRUE THEN
	        #fSetpoint := #sTag.fManualSetpoint;
	    ELSE
	        #fSetpoint := #sTag.fSetpoint;
	    END_IF;
	
	    // Enabled when enabled from process and from HMI (Auto), or in Manual mode
	    #Enabled := (#bEnable AND #sTag.bEnable) OR #sTag.bManualMode;
	
	    // Run controller
	    IF #Enabled = TRUE AND #sPV.bError = FALSE THEN
	        #Controller(fValue := #sPV.fValue, fLimit := #fSetpoint, fHysteresis := #sTag.fHysteresis, bType := #Above, bOutput => #sTag.bOutput );
	        #sTag.bAtSetpoint := NOT(#sTag.bOutput);
	    ELSE
	        #sTag.bAtSetpoint := FALSE;
	        #sTag.bOutput  := FALSE;
	    END_IF;
	
	    // Controller fault
	    #sTag.bFault := #sPV.bError;
	
	END_IF;
	
	// Mode
	IF #sTag.bManualOverride = TRUE THEN
	    #sTag.nMode := 3;
	ELSIF #sTag.bManualMode = TRUE THEN
	    #sTag.nMode := 2;
	ELSIF #sTag.bEnable = TRUE THEN
	    #sTag.nMode := 1;
	ELSE
	    #sTag.nMode := 0;
	END_IF;
	
	// Local outputs
	#bOutput := #sTag.bOutput;
	#bAtSetpoint := #sTag.bAtSetpoint;
	
	
END_FUNCTION_BLOCK

