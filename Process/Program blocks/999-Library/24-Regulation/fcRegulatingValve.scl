FUNCTION "fcRegulatingValve" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      fPositionCmd : Real;
      bForceClosed : Bool;   // 1 = force closed, 0 = do not force closed
      bReset : Bool;   // 1 = reset manual mode
   END_VAR

   VAR_IN_OUT 
      sTag : "stRegulatingValve";
   END_VAR


BEGIN
	// Regulating valve
	
	// Reset manual mode
	IF #bReset THEN
	    #sTag.Cmd.bManualMode := FALSE;
	    #sTag.Cmd.fManualValue := 0.0;
	END_IF;
	
	IF #sTag.Cmd.bManualMode = TRUE THEN
	    #sTag.Cmd.Position.Eng.fValue := #sTag.Cmd.fManualValue;
	ELSIF #bForceClosed = TRUE THEN
	    #sTag.Cmd.Position.Eng.fValue := #sTag.Cmd.fOffset;
	ELSIF #sTag.Cmd.fOffset = 0.0 THEN
	    #sTag.Cmd.Position.Eng.fValue := #fPositionCmd;
	ELSE
	    // squeeze [fMin,fMax] into the range [fOffset,fMax] to account for a nonzero closed position but still reach fully open at 100%
	    #sTag.Cmd.Position.Eng.fValue := "fcRescale"(InMin := #sTag.Cmd.Position.Eng.fMin, 
	                                                InMax  := #sTag.Cmd.Position.Eng.fMax,
	                                                InVal  := #fPositionCmd,
	                                                OutMin := #sTag.Cmd.fOffset,
	                                                OutMax := #sTag.Cmd.Position.Eng.fMax);
	END_IF;
END_FUNCTION

